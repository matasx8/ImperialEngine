#version 450

#extension GL_EXT_shader_16bit_storage: require
#extension GL_EXT_shader_8bit_storage: require
#extension GL_NV_mesh_shader: require
#extension GL_GOOGLE_include_directive: require
#extension GL_ARB_shader_draw_parameters: require
#extension GL_EXT_nonuniform_qualifier : require

//#include "prefix.h"
#include "DescriptorSet0.h"
#include "DescriptorSet1.h"

layout(local_size_x = 32, local_size_y = 1, local_size_z = 1) in;

out taskNV block
{
	uint meshletIndices[32];
};

bool is_inside_view_frustum(uint idx)
{
    BoundingVolume bv = meshlets[idx].boundingVolume;

    vec4 mCenter = vec4(bv.center, 1.0); // BV center in Model space
    vec4 wCenter = drawData[drawDataIndices[gl_DrawIDARB]].Transform * mCenter;           // BV center in World space

    float diameter = bv.diameter;

    for (int i = 0; i < 6; i++)
    {
        // Compute signed distance of center of BV from plane.
        // Plane equation: Ax + By + Cy + d = 0
        // Inserting our point into equation gives us the signed distance for wCenter
        float signedDistance = dot(globals.frustum[i], wCenter);

        // Negative result already lets us know that point is in negative half space of plane
        // If it's less than 0 + (-diameter) then BV is outside VF
        if(signedDistance < -diameter)
            return false;
        
        // Temporary solution to saving distance from near plane for LOD picking
        //if(i == 4)
        //    distFromCamera = signedDistance - diameter;
    }
    return true;
}

/**
 * For backface culling with orthographic projection, use the following formula to reject backfacing clusters:
 *   dot(view, cone_axis) >= cone_cutoff
 *
 * For perspective projection, you can the formula that needs cone apex in addition to axis & cutoff:
 *   dot(normalize(cone_apex - camera_position), cone_axis) >= cone_cutoff
 */
 // Above snippet from meshoptimizer.h
 // apparent this is wrong for perspective projection as I suspected
 // look at RT-rendering 19.3 for more info

 // TODO: backface cluster culling is not 100% correct. Haven't figured out what to do when mesh is half clipped
bool coneCull(vec4 cone, vec3 view)
{
	return dot(cone.xyz, view) > cone.w;
}

shared uint meshletCount;

void main()
{
	uint ti = gl_LocalInvocationID.x;
	uint mgi = gl_WorkGroupID.x;
	uint mi = mgi * 32 + ti;

	meshletCount = 0;

	memoryBarrierShared();

	vec4 decoded_cone = vec4(uint(meshlets[mi].cone[0]), uint(meshlets[mi].cone[1]), uint(meshlets[mi].cone[2]), uint(meshlets[mi].cone[3])) / 127.0;

	// Transform meshlet normal cone to clip space
	vec4 cone_model_space = vec4(decoded_cone.xyz, 1);
	mat4 model_matrix = drawData[drawDataIndices[gl_DrawIDARB]].Transform;
	vec4 cone_clip_space = globals.PV * model_matrix * cone_model_space;
	 
	// In Vulkan clip space the camera view direction is -z
	vec3 view_dir_clip_space = vec3(0, 0, -1);

	// w component is cone half angle
	vec4 cone = vec4(cone_clip_space.xyz, decoded_cone.w);

	bool visible = !coneCull(cone, view_dir_clip_space);
	bool is_inside_VF = false;

	if (visible)
		is_inside_VF = is_inside_view_frustum(mi);

	if(visible && is_inside_VF)
	{
		uint index = atomicAdd(meshletCount, 1);

		meshletIndices[index] = mi;
	}

	memoryBarrierShared();

	if (ti == 0)
	{
		uint drawDataIndex = drawDataIndices[gl_DrawIDARB];
		uint taskCount = min(meshletCount, drawData[drawDataIndex].taskCount - gl_WorkGroupID.x * 32);

		gl_TaskCountNV = taskCount;
	}
}